# cd3b7dc0391c950cab95aed8444bc57173d228cac9dd6615a8629eab10785ad6 Elan 2.0.0-alpha guest default_profile valid

function bestFitLine(points as List<of Point>) returns (Float, Float)
  constant sumX set to points.reduce(0.0, lambda s as Float, p as Point => s + p.x)
  constant sumXsq set to points.reduce(0.0, lambda s as Float, p as Point => s + power(p.x, 2))
  constant sumY set to points.reduce(0.0, lambda s as Float, p as Point => s + p.y)
  constant sumXY set to points.reduce(0.0, lambda s as Float, p as Point => s + p.x*p.y)
  constant n set to points.length()
  constant a set to (sumY*sumXsq - sumX*sumXY)/(n*sumXsq - sumX*sumX)
  constant b set to (n*sumXY - sumX*sumY)/(n*sumXsq - sumX*sumX)
  return tuple(a, b)
end function

class Point
  property x as Float

  property y as Float

  constructor(x as Float, y as Float)
    set property.x to x
    set property.y to y
  end constructor

end class

function newPoint(x as Float, y as Float) returns Point
  return new Point(x, y)
end function

test test_
  variable l1 set to [newPoint(0.71, 1.12), newPoint(3.56, 5.36), newPoint(7.83, 9.04)]
  variable a_b set to bestFitLine(l1)
  constant a set to a_b.item0
  constant b set to a_b.item1
  assert a.round(3) is 0.766
  assert b.round(3) is 1.093
end test
