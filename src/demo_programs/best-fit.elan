# 82e9737328fd970dc22a4d3064f0cddac0c4dfb29a5c8b0702bfb65fd15db199 Elan 1.1.2 guest default_profile valid

function bestFitLine(points as List<of Point>) returns (Float, Float)
  let sumX be points.reduce(0.0, lambda s as Float, p as Point => s + p.x)
  let sumXsq be points.reduce(0.0, lambda s as Float, p as Point => s + p.x^2)
  let sumY be points.reduce(0.0, lambda s as Float, p as Point => s + p.y)
  let sumXY be points.reduce(0.0, lambda s as Float, p as Point => s + p.x*p.y)
  let n be points.length()
  let a be (sumY*sumXsq - sumX*sumXY)/(n*sumXsq - sumX*sumX)
  let b be (n*sumXY - sumX*sumY)/(n*sumXsq - sumX*sumX)
  return tuple(a, b)
end function

record Point
  property x as Float

  property y as Float

end record

function newPoint(x as Float, y as Float) returns Point
  return new Point() with x set to x, y set to y
end function

test 
  let l1 be [newPoint(0.71, 1.12), newPoint(3.56, 5.36), newPoint(7.83, 9.04)]
  let first, second be bestFitLine(l1)
  assert first.round(3) is 0.766
  assert second.round(3) is 1.093
end test
