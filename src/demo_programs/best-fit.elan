# b47805aa45e5caca0fd8fb219728c008f72751c86555037a604c8c772908eb9b Elan 2.0.0-alpha guest default_profile valid

function bestFitLine(points as List<of Point>) returns (Float, Float)
  constant sumX set to points.reduce(0.0, lambda s as Float, p as Point => s + p.x)
  constant sumXsq set to points.reduce(0.0, lambda s as Float, p as Point => s + power(p.x, 2))
  constant sumY set to points.reduce(0.0, lambda s as Float, p as Point => s + p.y)
  constant sumXY set to points.reduce(0.0, lambda s as Float, p as Point => s + p.x*p.y)
  constant n set to points.length()
  constant a set to (sumY*sumXsq - sumX*sumXY)/(n*sumXsq - sumX*sumX)
  constant b set to (n*sumXY - sumX*sumY)/(n*sumXsq - sumX*sumX)
  return tuple(a, b)
end function

record Point
  property x as Float

  property y as Float

end record

function newPoint(x as Float, y as Float) returns Point
  return new Point() with x set to x, y set to y
end function

test test_
  constant l1 set to [newPoint(0.71, 1.12), newPoint(3.56, 5.36), newPoint(7.83, 9.04)]
  constant a_b set to bestFitLine(l1)
  constant a set to a_b.item0
  constant b set to a_b.item1
  assert a.round(3) is 0.766
  assert b.round(3) is 1.093
end test
