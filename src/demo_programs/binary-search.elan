# fd2c555a9d82def6c7222c5b4709bbec75c796ba6944072e13a9fe53e67a3d09 Elan 2.0.0-alpha guest default_profile valid

main
  variable fruit set to ["apple", "avocado", "banana", "blueberry", "cherry", "fig", "grape", "kiwi", "lemon", "lychee", "mango", "orange", "papaya", "peach", "pear", "pineapple", "plum", "raspberry", "strawberry", "watermelon"]
  variable exit set to false
  while not exit
    variable wanted set to inputString("What type of fruit do you want ('x' to exit)? ")
    if wanted is "x" then
      set exit to true
    else
      variable result set to binarySearch(fruit, wanted)
      if result then
        call print("We can supply a {wanted}")
      else
        call print("Sorry, we cannot supply a {wanted}")
      end if
    end if
  end while
end main

function binarySearch(li as List<of String>, item as String) returns Boolean
  variable result set to false
  if li.length() > 0 then
    constant mid set to (li.length()/2).floor()
    variable value set to li[mid]
    if item is value then
      set result to true
    elif item.isBefore(value) then
      set result to binarySearch(li[..mid], item)
    else
      set result to binarySearch(li[mid + 1..], item)
    end if
  end if
  return result
end function

test test_
  constant li1 set to ["lemon", "lime", "orange"]
  assert binarySearch(li1, "lemon") is true
  assert binarySearch(li1, "lime") is true
  assert binarySearch(li1, "orange") is true
  assert binarySearch(li1, "pear") is false
  constant li2 set to ["lemon", "orange"]
  assert binarySearch(li2, "lemon") is true
  assert binarySearch(li2, "orange") is true
  assert binarySearch(li2, "pear") is false
  constant li3 set to ["lemon"]
  assert binarySearch(li3, "lemon") is true
  assert binarySearch(li3, "lime") is false
  constant li4 set to empty List<of String>
  assert binarySearch(li4, "pear") is false
end test
