# 22771067aa4bde6b1b359b80539c68a7dd6687be6d417e7d83cf0aaf998dc6e6 Elan 1.1.2 guest default_profile valid

# Demo shows one way to implement a mergeSort function - using recursion
# Since merge sort is always an 'out of place' sort, we have chosen to use immutable lists
# It could be written with mutable lists, but the code for the merge function would need changing
main
  let fruit be ["plum", "apricot", "lime", "lemon", "melon", "apple", "orange", "strawberry", "pear"]
  print "Original: {fruit}"
  print "Sorted: {sort(fruit)}"
end main

function sort(li as List<of String>) returns List<of String>
  variable result set to li
  let len be li.length()
  if len > 1 then
    let mid be (len/2).floor()
    let frontHalf be sort(li[..mid])
    let backHalf be sort(li[mid..])
    set result to merge(frontHalf, backHalf)
  end if
  return result
end function

function merge(a as List<of String>, b as List<of String>) returns List<of String>
  variable result set to empty List<of String>
  if a.length() is 0 then
    set result to b
  else if b.length() is 0 then
    set result to a
  else if a[0].isBefore(b[0]) then
    set result to [a[0]].withAppendList(merge(a[1..], b))
  else
    set result to [b[0]].withAppendList(merge(a, b[1..]))
  end if
  return result
end function

test merge
  let emptyList be empty List<of String>
  assert merge(emptyList, emptyList) is emptyList
  assert merge(["orange"], ["melon"]) is ["melon", "orange"]
  let l1 be ["apple", "lime", "pear"]
  let l2 be ["apricot", "lemon", "plum"]
  assert merge(l1, l2) is ["apple", "apricot", "lemon", "lime", "pear", "plum"]
  assert merge(l2, l1) is ["apple", "apricot", "lemon", "lime", "pear", "plum"]
  assert merge(l1, emptyList) is l1
  assert merge(emptyList, l2) is l2
  assert merge(l1, l1) is ["apple", "apple", "lime", "lime", "pear", "pear"]
end test

test sort
  let emptyList be empty List<of String>
  assert sort(emptyList) is emptyList
  let li2 be ["plum"]
  assert sort(li2) is li2
  let li3 be ["plum", "apricot", "lime", "lemon", "melon", "apple", "orange", "strawberry", "pear"]
  let sorted3 be ["apple", "apricot", "lemon", "lime", "melon", "orange", "pear", "plum", "strawberry"]
  assert sort(li3) is sorted3
  let li4 be ["plum", "apricot", "lime", "lemon", "melon", "apple", "orange", "strawberry"]
  let sorted4 be ["apple", "apricot", "lemon", "lime", "melon", "orange", "plum", "strawberry"]
  assert sort(li4) is sorted4
  let li5 be ["apple", "apricot", "lemon", "lime", "melon", "orange", "pear", "strawberry"]
  assert sort(li5) is li5
end test
