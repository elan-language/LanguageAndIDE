# a31b40ce0d5c8522baf99d99944998c8ebfae61e8318daffdfb2c8125aaefe93 Elan 2.0.0-alpha guest default_profile valid

main
  variable grid set to createBlockGraphics()
  call fillRandom(grid)
  while true
    call displayBlocks(grid)
    variable gridRef set to new Ref<of Array2D<of Int>>(grid)
    call nextGeneration(gridRef)
    set grid to gridRef.value()
    call pause(50)
  end while
end main

procedure fillRandom(grid as Array2D<of Int>)
  for col from 0 to 39 step 1
    for row from 0 to 29 step 1
      call grid.put(col, row, blackOrWhite(random()))
    end for
  end for
end procedure

function blackOrWhite(random as Float) returns Int
  variable result set to black
  if random > 0.5 then
    set result to white
  end if
  return result
end function

function north(cell as (Int, Int)) returns (Int, Int)
  constant x set to cell.item0
  constant y set to cell.item1
  constant y2 set to if y is 0 then 29 else y - 1
  return tuple(x, y2)
end function

function south(cell as (Int, Int)) returns (Int, Int)
  constant x set to cell.item0
  constant y set to cell.item1
  constant y2 set to if y is 29 then 0 else y + 1
  return tuple(x, y2)
end function

function east(cell as (Int, Int)) returns (Int, Int)
  constant x set to cell.item0
  constant y set to cell.item1
  constant x2 set to if x is 39 then 0 else x + 1
  return tuple(x2, y)
end function

function west(cell as (Int, Int)) returns (Int, Int)
  constant x set to cell.item0
  constant y set to cell.item1
  constant x2 set to if x is 0 then 39 else x - 1
  return tuple(x2, y)
end function

function northEast(cell as (Int, Int)) returns (Int, Int)
  return north(east(cell))
end function

function northWest(cell as (Int, Int)) returns (Int, Int)
  return north(west(cell))
end function

function southEast(cell as (Int, Int)) returns (Int, Int)
  return south(east(cell))
end function

function southWest(cell as (Int, Int)) returns (Int, Int)
  return south(west(cell))
end function

function neighbourCells(x as Int, y as Int) returns List<of (Int, Int)>
  variable c set to tuple(x, y)
  return [northWest(c), north(c), northEast(c), west(c), east(c), southWest(c), south(c), southEast(c)]
end function

function liveNeighbours(grid as Array2D<of Int>, x as Int, y as Int) returns Int
  variable count set to 0
  each cell in neighbourCells(x, y)
    constant cx set to cell.item0
    constant cy set to cell.item1
    if grid[cx, cy] is black then
      set count to count + 1
    end if
  end each
  return count
end function

function willLive(cell as Int, liveNeighbours as Int) returns Boolean
  variable result set to false
  if cell is black then
    set result to (liveNeighbours > 1) and (liveNeighbours < 4)
  else
    set result to liveNeighbours is 3
  end if
  return result
end function

function nextCellValue(grid as Array2D<of Int>, x as Int, y as Int) returns Int
  variable colour set to white
  constant live set to willLive(grid[x, y], liveNeighbours(grid, x, y))
  if live then
    set colour to black
  end if
  return colour
end function

procedure nextGeneration(gridRef as Ref<of Array2D<of Int>>)
  variable next set to new Array2D<of Int>(40, 30, white)
  variable grid set to gridRef.value()
  for x from 0 to 39 step 1
    for y from 0 to 29 step 1
      constant colour set to nextCellValue(grid, x, y)
      call next.put(x, y, colour)
    end for
  end for
  call gridRef.set(next)
end procedure

test test_north
  assert north(tuple(3, 4)) is tuple(3, 3)
  assert north(tuple(39, 0)) is tuple(39, 29)
  assert north(tuple(0, 29)) is tuple(0, 28)
  assert north(tuple(39, 29)) is tuple(39, 28)
end test

test test_south
  assert south(tuple(3, 4)) is tuple(3, 5)
  assert south(tuple(39, 0)) is tuple(39, 1)
  assert south(tuple(0, 29)) is tuple(0, 0)
  assert south(tuple(39, 29)) is tuple(39, 0)
end test

test test_east
  assert east(tuple(10, 2)) is tuple(11, 2)
  assert east(tuple(39, 0)) is tuple(0, 0)
  assert east(tuple(0, 1)) is tuple(1, 1)
  assert east(tuple(39, 29)) is tuple(0, 29)
end test

test test_west
  assert west(tuple(3, 4)) is tuple(2, 4)
  assert west(tuple(39, 0)) is tuple(38, 0)
  assert west(tuple(0, 0)) is tuple(39, 0)
  assert west(tuple(0, 29)) is tuple(39, 29)
end test

test test_northEast
  assert northEast(tuple(3, 4)) is tuple(4, 3)
  assert northEast(tuple(0, 0)) is tuple(1, 29)
  assert northEast(tuple(39, 0)) is tuple(0, 29)
  assert northEast(tuple(0, 29)) is tuple(1, 28)
  assert northEast(tuple(39, 29)) is tuple(0, 28)
end test

test test_southEast
  assert southEast(tuple(3, 4)) is tuple(4, 5)
  assert southEast(tuple(0, 0)) is tuple(1, 1)
  assert southEast(tuple(39, 0)) is tuple(0, 1)
  assert southEast(tuple(0, 29)) is tuple(1, 0)
  assert southEast(tuple(39, 29)) is tuple(0, 0)
end test

test test_northWest
  assert northWest(tuple(3, 4)) is tuple(2, 3)
  assert northWest(tuple(0, 0)) is tuple(39, 29)
  assert northWest(tuple(39, 0)) is tuple(38, 29)
  assert northWest(tuple(0, 29)) is tuple(39, 28)
  assert northWest(tuple(39, 29)) is tuple(38, 28)
end test

test test_southWest
  assert southWest(tuple(3, 4)) is tuple(2, 5)
  assert southWest(tuple(0, 0)) is tuple(39, 1)
  assert southWest(tuple(39, 0)) is tuple(38, 1)
  assert southWest(tuple(0, 29)) is tuple(39, 0)
  assert southWest(tuple(39, 29)) is tuple(38, 0)
end test

test test_blackOrWhite
  assert blackOrWhite(0) is black
  assert blackOrWhite(0.499) is black
  assert blackOrWhite(0.5) is black
  assert blackOrWhite(0.501) is white
  assert blackOrWhite(1) is white
end test

test test_neighbourCells
  assert neighbourCells(3, 4) is [tuple(2, 3), tuple(3, 3), tuple(4, 3), tuple(2, 4), tuple(4, 4), tuple(2, 5), tuple(3, 5), tuple(4, 5)]
end test

test test_willLive
  assert willLive(white, 0) is false
  assert willLive(white, 1) is false
  assert willLive(white, 2) is false
  assert willLive(white, 3) is true
  assert willLive(white, 4) is false
  assert willLive(white, 5) is false
  assert willLive(white, 6) is false
  assert willLive(white, 7) is false
  assert willLive(white, 8) is false
  assert willLive(black, 0) is false
  assert willLive(black, 1) is false
  assert willLive(black, 2) is true
  assert willLive(black, 3) is true
  assert willLive(black, 4) is false
  assert willLive(black, 5) is false
  assert willLive(black, 6) is false
  assert willLive(black, 7) is false
  assert willLive(black, 8) is false
end test
