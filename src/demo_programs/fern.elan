# a04663415d2135a634108824328a49a2373096d6319cf0cd4e2942a150fd31dd Elan 2.0.0-alpha guest default_profile valid

# https://en.wikipedia.org/wiki/Barnsley_fern
# Draws a fractal image
main
  variable vg set to createListOfVectorGraphics()
  # coordinates
  variable x set to 0.0
  variable y set to 0.16
  # random number
  variable r set to new Random()
  variable rect set to new RectangleVG()
  variable count set to 0
  # don't set the count limit too high
  # or the browser will run out of memory
  while count < 1000
    set rect, x, y, r to onepoint(x, y, r)
    call vg.append(rect)
    call displayVectorGraphics(vg)
    set count to count + 1
  end while
  call print("Finished")
end main

function onepoint(x as Float, y as Float, r as Random) returns (RectangleVG, Float, Float, Random)
  # next pseudo-random number (Grogono parameters)
  constant rValue, rNext set to r.next()
  constant nx, ny set to onestep(x, y, rValue)
  constant rect set to new RectangleVG() with x set to nx*scale + 50, y set to 75 - ny*scale, width set to 0.5, height set to 0.5, fillColour set to 0x408040, strokeColour set to 0x408040, strokeWidth set to 0.25
  return tuple(rect, nx, ny, rNext)
end function

# r is random Float 0.0 <= r < 1.0
# returns new x and y
# written in a data-driven style
function onestep(x as Float, y as Float, r as Float) returns (Float, Float)
  # Currently you can't make the params into one long line
  # as it makes the browser use 100% CPU
  # xx xy yx yy cx cy probablity
  variable p1 set to [0.0, 0.0, 0.0, 0.16, 0.0, 0.0, 0.01]
  variable p2 set to [0.85, 0.04, -0.04, 0.85, 0.0, 1.60, 0.85]
  variable p3 set to [0.20, -0.26, 0.23, 0.22, 0.0, 1.60, 0.07]
  variable p4 set to [-0.15, 0.28, 0.26, 0.24, 0.0, 0.44, 0.07]
  variable params set to [p1, p2, p3, p4]
  # cumulative probabilty
  variable cumuprob set to 0.0
  variable done set to false
  variable nx set to 0.0
  variable ny set to 0.0
  each pp in params
    set cumuprob to cumuprob + pp[6]
    if (not done) and (r < cumuprob) then
      set nx to x*pp[0] + y*pp[1] + pp[4]
      set ny to x*pp[2] + y*pp[3] + pp[5]
      set done to true
    end if
  end each
  return tuple(nx, ny)
end function

test test_one
  assert roundtuple2(onestep(0.0, 0.16, 0.5)) is [0.0064, 1.736]
  assert roundtuple2(onestep(0.0, 0.16, 0.9)) is [-0.0416, 1.6352]
end test

# two approaches to rounding a tuple to N decimal places
function roundtuple1(n as (Float, Float)) returns (Float, Float)
  constant a, b set to n
  return tuple(a.round(8), b.round(8))
end function

function roundtuple2(n as (Float, Float)) returns List<of Float>
  constant a, b set to n
  return [a, b].map(lambda x as Float => x.round(8))
end function

constant scale set to 7
