# 2daa58a489afdc50dc7df292031294d9348bfd3b9412c2cda13d3c37b17e9ed5 Elan 2.0.0-alpha guest default_profile valid

main
  variable ok set to true
  variable interval set to 1000
  variable sequence set to new List<of Int>()
  while ok
    call simonsTurn(sequence, interval)
    call clearVectorGraphics()
    call pause(1000)
    call playersTurn(sequence, interval, ok)
    call clearVectorGraphics()
  end while
  call print("Game over. You scored: {sequence.length() - 1}")
end main

procedure simonsTurn(sequence as List<of Int>, interval as Int)
  call sequence.append(randomInt(1, 4))
  each move in sequence
    call displayMove(move, interval)
  end each
end procedure

procedure playersTurn(sequence as List<of Int>, interval as Int, out ok as Boolean)
  variable counter set to 0
  variable correctSoFar set to true
  while (counter < sequence.length()) and correctSoFar
    variable k set to waitForKey()
    constant validMove set to parseAsInt(k)
    constant valid set to validKey.item1
    constant move set to validMove.item2
    if valid and (sequence[counter] is move) then
      call displayMove(move, interval)
    else
      set correctSoFar to false
      set ok to false
    end if
    set counter to counter + 1
  end while
end procedure

procedure displayMove(move as Int, interval as Int)
  variable colours set to [red, yellow, blue, green]
  variable tones set to [164.81, 220, 277.18, 329.63]
  variable circle set to new CircleVG() with centreX set to move*20 - 5, centreY set to 15, radius set to 10, strokeWidth set to 0, fillColour set to colours[move - 1]
  call displayVectorGraphics([circle])
  call tone(interval, tones[move - 1], 0.1)
  call clearVectorGraphics()
  call pause(100)
end procedure
