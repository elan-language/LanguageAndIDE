# 48219c22fe23e4b6aba3951545e0b3be53a331412414c5a2c9303fdeebea3568 Elan 1.9.0 guest default_profile valid

main
  variable score set to 0
  while true
    variable hole set to Hole.vacant
    if random() > 0.5 then
      set hole to Hole.mole
    end if
    let html be htmlForGame(hole, score)
    call displayHtml(html)
    let stop be clock() + 1000
    while clock() < stop
      call processKey(hole, score)
    end while
  end while
end main

procedure processKey(out hole as Hole, out score as Int)
  let k be getNumericKey()
  if k isnt -1 then
    if hole is Hole.mole then
      set hole to Hole.hit
      set score to score + 1
    else
      set hole to Hole.miss
      set score to score - 1
    end if
    let html be htmlForGame(hole, score)
    call displayHtml(html)
  end if
end procedure

function htmlForGame(hole as Hole, score as Int) returns String
  return "<style>{styleSheet}</style>{htmlForHole(hole, "hole")}<div class='score'>Score: {score}</score>"
end function

# TODO test for htmlForGame 
# TODO ghosted tests for htmlForGame (using Array)
function htmlForHole(hole as Hole, label as String) returns String
  let img be imageForStatus[hole]
  return "<div class='hole'><img src='{img}'><div class='label'>{label}</div></div>"
end function

test htmlForHole
  assert htmlForHole(Hole.vacant, "a") is "<div class='hole'><img src='https://elan-lang.org/documentation/worksheets/whack-a-mole/vacant.png'><div class='label'>a</div></div>"
  assert htmlForHole(Hole.mole, "b") is "<div class='hole'><img src='https://elan-lang.org/documentation/worksheets/whack-a-mole/mole.png'><div class='label'>b</div></div>"
  assert htmlForHole(Hole.hit, "c") is "<div class='hole'><img src='https://elan-lang.org/documentation/worksheets/whack-a-mole/hit.png'><div class='label'>c</div></div>"
  assert htmlForHole(Hole.miss, "d") is "<div class='hole'><img src='https://elan-lang.org/documentation/worksheets/whack-a-mole/miss.png'><div class='label'>d</div></div>"
end test

enum Hole vacant, mole, hit, miss

# Acknowledgement:  mole icon source https://www.flaticon.com/
constant vacantImg set to "https://elan-lang.org/documentation/worksheets/whack-a-mole/vacant.png"

constant moleImg set to "https://elan-lang.org/documentation/worksheets/whack-a-mole/mole.png"

constant hitImg set to "https://elan-lang.org/documentation/worksheets/whack-a-mole/hit.png"

constant missImg set to "https://elan-lang.org/documentation/worksheets/whack-a-mole/miss.png"

constant imageForStatus set to {Hole.vacant:vacantImg, Hole.mole:moleImg, Hole.hit:hitImg, Hole.miss:missImg}

constant styleSheet set to '\n.game { display: flex; flex-direction: row; }\n.hole { display: flex; flex-direction: column; width: 50px; height: 50px; border: 10px solid white;}\n.label {width: 40px; text-align: center;}\n.hole img {height: 40px; width: 40px}\n.score{ margin-top: 20px;}'
