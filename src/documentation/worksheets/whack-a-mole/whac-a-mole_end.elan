# d5463691b9a02ba35cdc7691e0b2e442fcf97aecb8c74189ab7eacf5ee1f43f1 Elan 1.9.0 guest default_profile valid

main
  variable score set to 0
  while true
    variable holes set to new Array<of Hole>(10, Hole.vacant)
    variable hole set to Hole.vacant
    let numberOfMoles be randomInt(1, 3)
    for count from 1 to numberOfMoles step 1
      let holeNumber be randomInt(0, 9)
      call holes.put(holeNumber, Hole.mole)
    end for
    let html be htmlForFullGame(holes, score)
    call displayHtml(html)
    let stop be clock() + 1000
    while clock() < stop
      call processKey(holes, score)
    end while
  end while
end main

procedure processKey(out holes as Array<of Hole>, out score as Int)
  let n be getNumericKey()
  if n isnt -1 then
    if holes[n] is Hole.mole then
      call holes.put(n, Hole.hit)
      set score to score + 1
    else if holes[n] is Hole.vacant then
      call holes.put(n, Hole.miss)
      set score to score - 1
    end if
    let html be htmlForFullGame(holes, score)
    call displayHtml(html)
  end if
end procedure

[imported] enum Hole vacant, mole, hit, miss

[imported] function htmlForSimpleGame(hole as Hole, score as Int) returns String
  let core be htmlForHole(hole, "hole")
  return "<style>{styleSheet}</style><div class='game'>{core}</div><div class='score'>Score: {score}</score>"
end function

[imported] function htmlForFullGame(holes as Array<of Hole>, score as Int) returns String
  variable core set to ""
  for i from 0 to holes.length() - 1 step 1
    set core to core + htmlForHole(holes[i], i.asString())
  end for
  return "<style>{styleSheet}</style><div class='game'>{core}</div><div class='score'>Score: {score}</score>"
end function

[imported] function htmlForHole(hole as Hole, label as String) returns String
  let img be imageForStatus[hole]
  return "<div class='hole'><img src='{img}'><div class='label'>{label}</div></div>"
end function

# Acknowledgement:  mole icon source https://www.flaticon.com/
[imported] constant vacantImg set to "https://elan-lang.org/documentation/worksheets/whack-a-mole/vacant.png"

[imported] constant moleImg set to "https://elan-lang.org/documentation/worksheets/whack-a-mole/mole.png"

[imported] constant hitImg set to "https://elan-lang.org/documentation/worksheets/whack-a-mole/hit.png"

[imported] constant missImg set to "https://elan-lang.org/documentation/worksheets/whack-a-mole/miss.png"

[imported] constant imageForStatus set to {Hole.vacant:vacantImg, Hole.mole:moleImg, Hole.hit:hitImg, Hole.miss:missImg}

[imported] constant styleSheet set to '\n.game { display: flex; flex-direction: row; }\n.hole { display: flex; flex-direction: column; width: 50px; height: 50px; border: 10px solid white;}\n.label {width: 40px; text-align: center;}\n.hole img {height: 40px; width: 40px}\n.score{ margin-top: 20px;}'
