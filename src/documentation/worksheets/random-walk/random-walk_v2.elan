# 3ab2857d0ef4ea62c5e38e5467d9b35037479e454550de14a3e3de7b3505474c Elan 1.9.0 guest default_profile valid

main
  let t be new Turtle()
  let radius be 50
  call drawCircle(t, 0, 0, radius)
  call t.hide()
  variable totalPaces set to 0
  let numberOfWalks be 100
  for i from 1 to numberOfWalks step 1
    call drawRandomWalk(t, radius, totalPaces)
  end for
  let average be (totalPaces/numberOfWalks).round(2)
  print "Over {numberOfWalks} attempts, the average no. of paces to edge was {average}"
end main

procedure drawCircle(t as Turtle, x as Int, y as Int, radius as Int)
  call t.placeAt(x - radius, 0)
  let segment be 2*radius*pi/360
  for i from 1 to 360 step 1
    call t.move(segment)
    call t.turn(1)
  end for
end procedure

procedure drawRandomPace(t as Turtle, pace as Int)
  call t.move(pace)
  let randomTurn be randomInt(-90, 90)
  call t.turn(randomTurn)
end procedure

procedure drawRandomWalk(t as Turtle, radius as Int, out totalPaces as Int)
  call t.placeAt(0, 0)
  let pace be 5
  call t.penColour(randomInt(black, white))
  variable pacesThisAttempt set to 0
  while sqrt(t.x^2 + t.y^2) < radius
    call drawRandomPace(t, pace)
    set pacesThisAttempt to pacesThisAttempt + 1
  end while
  set totalPaces to totalPaces + pacesThisAttempt
end procedure
