<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN" "http://www.w3.org/TR/html4/loose.dtd">
<html lang="en-GB">

  <head>
      <meta http-equiv="Cache-Control" content="no-store" />
      <link href="https://elan-language.github.io/LanguageAndIDE/styles/colourScheme.css" rel="stylesheet" />
      <link href="https://elan-language.github.io/LanguageAndIDE/styles/elanStyle.css" rel="stylesheet" />
      <link href="https://elan-language.github.io/LanguageAndIDE/styles/documentation.css" rel="stylesheet" />
      <link href="https://elan-language.github.io/LanguageAndIDE/styles/worksheet.css" rel="stylesheet" />
      <title>Blackjack 1: exploring the Blackjack program and making small changes</title>
  </head>

  <body>
    <div id="worksheet">
    <div class="docTitle">Blackjack 1: exploring the Blackjack program and making small changes</div>

      <p class="transient">Choose <i>either one</i> of the two options below:</p>

      <p><b>Option 1: Start this worksheet from scratch</b></p>

      <ul>
        <li>First <label for="username">Enter your name: </label><input class="question" type="text" id="username" /></li>
        <li>Then <button id="auto-save">Create and save a new answers file</button>.
      </ul>

      <p><b>Option 2: Continue, or view, previous work</b></p>

      <ul>
        <li><button id="load-answers">Load a previously-saved (.json) answers file</button>
        <li>Immediately after loading, you will immediately be asked to confirm that you want to save any further edits to this file.</li>
        (If you don't want to do that, hit <b>Cancel</b> on the dialog to return to these two options. Note that you
        may make a copy of your answers file outside of the Elan IDE.)
      </ul>

      <p>In either case, any edits that you subsequently make in the worksheet will be auto-saved to the file.</p>
     
      <p>Set the browser to <b>Full Screen</b> view, to give this worksheet and your code as much space as possible.</p>

    <div class="license">
      <img decoding="async" loading="lazy" src="https://mirrors.creativecommons.org/presskit/buttons/88x31/png/by-nc-nd.png" width="118" height="41">
      This worksheet is copyright &copy; Richard Pawson 2025, and protected by the Creative Commons license:
          <a href="https://creativecommons.org/licenses/by-nc-nd/4.0/" target="_blank"> Attribution-NonCommercial-NoDerivatives 4.0 International</a>.
        You may freely make and distribute copies of this worksheet <i>as is</i>, but if you <i>modify</i> this worksheet you may not distribute your modified version (outside your own teaching institution) without the author's permission.
        Please <a href="mailto:rpawson@nakedobjects.org" target="_blank">email the author</a> to report errors or suggest improvements.
    </div> 
      
<div class="step" id="step1">
    <h1>Step 1: learning the rules of Blackjack</h1>
    <p>Your first task is to play a few rounds of Blackjack, to make sure that you understand how the game
      works, and - since there are many minor variations on the game - the rules of this specific implementation.</p>
    <p><button class="load" id="load1-0">Load the starting code</button></p><div id="code-load1-0" hidden=""># c050555cf758e8d118d38efe1bda75e3b304b41b3eda23c2b248cb2e2710b10b Elan 1.7.0 guest default_profile valid

main
  let game be new Game(1000)
  call game.addPlayer(new HumanPlayer("Player A", 1000))
  variable anotherRound set to true
  while anotherRound
    call playOneRound(game)
    call game.setMessage("Points updated. Do you want to play another round? (press y or n)")
    call clearKeyBuffer()
    call display(game)
    let k be waitForKey().lowerCase()
    if k is 'y' then
      call game.setMessage("")
    else
      set anotherRound to false
    end if
  end while
end main

procedure playOneRound(game as Game)
  call game.newRound()
  call display(game)
  let dealer be game.dealer
  each player in game.players
    call player.startTurn()
    call display(game)
    while player.status is Status.active
      call player.nextAction(dealer.faceCard)
      call display(game)
    end while
  end each
  call dealer.play()
  call display(game)
  while dealer.status is Status.active
    call dealer.nextAction(dealer.faceCard)
    call display(game)
  end while
  call game.updatePoints()
end procedure

procedure display(game as Game)
  let html be "&lt;style&gt;{styleSheet}&lt;/style&gt;{htmlForGame(game)}"
  call displayHtml(html)
  call pause(1500)
end procedure

procedure updatePoints(dealer as Dealer, player as Player, playerOutcome as Outcome)
  if playerOutcome is Outcome.winDouble then
    call player.changePointsBy(2)
    call dealer.changePointsBy(-2)
  else if playerOutcome is Outcome.win then
    call player.changePointsBy(1)
    call dealer.changePointsBy(-1)
  else if playerOutcome is Outcome.lose then
    call player.changePointsBy(-1)
    call dealer.changePointsBy(1)
  end if
end procedure

function determinePlayerOutcome(dealer as Dealer, player as Player) returns Outcome
  # These 'let' definitions are just to make the logic that follows them simpler to read
  let d be dealer.status
  let dTotal be dealer.handTotal
  let p be player.status
  let pTotal be player.handTotal
  let bust be Status.bust
  let bj be Status.blackjack
  let win be Outcome.win
  let winDouble be Outcome.winDouble
  let lose be Outcome.lose
  let draw be Outcome.draw
  variable playerOutcome set to draw
  if p is bust then
    set playerOutcome to lose
  else if (p is bj) and (d isnt bj) then
    set playerOutcome to winDouble
  else if d is bust then
    set playerOutcome to win
  else if (d is bj) and (p is bj) then
    set playerOutcome to draw
  else if p is bj then
    set playerOutcome to winDouble
  else if d is bj then
    set playerOutcome to lose
  else if pTotal &gt; dTotal then
    set playerOutcome to win
  else if pTotal &lt; dTotal then
    set playerOutcome to lose
  else
    # strictly, this 'else' clause is redundant - as the variable was initialised to 'draw' - but added for clarity
    set playerOutcome to draw
  end if
  return playerOutcome
end function

function dealCard(random as Float) returns Card
  let number be (random*52).floor()
  let rank be rankValue.keys()[number div 4]
  let suit be number mod 4
  return new Card() with rank set to rank, suit set to intAsSuit(suit)
end function

function intAsSuit(n as Int) returns Suit
  variable suit set to Suit.clubs
  if n is 1 then
    set suit to Suit.diamonds
  else if n is 2 then
    set suit to Suit.hearts
  else if n is 3 then
    set suit to Suit.spades
  end if
  return suit
end function

function htmlForGame(game as Game) returns String
  variable html set to "&lt;div class='game'&gt;"
  set html to html + htmlForPlayer(game.dealer)
  each player in game.players
    set html to html + htmlForPlayer(player)
  end each
  set html to html + "&lt;div class='message'&gt;{game.message}&lt;/div&gt;"
  return html + "&lt;/div&gt;"
end function

function htmlForPlayer(player as Player) returns String
  variable html set to "&lt;div class='player'&gt;"
  set html to html + "&lt;div class='details'&gt;{player.name} - {player.points} points {player.getMessage()}&lt;/div&gt;"
  set html to html + "&lt;div class='hand'&gt;"
  each card in player.cards
    let suit be card.suit
    let rank be card.rank
    set html to html + htmlForCard(card)
  end each
  return html + "&lt;/div&gt;"
end function

function htmlForCard(card as Card) returns String
  variable html set to ""
  if card.faceDown then
    set html to "&lt;div class='card reversed'&gt;"
  else
    let rank be card.rank
    let suit be card.suit
    let colour be colours[suit]
    let symbol be symbols[suit]
    set html to "&lt;div class='card {colour}'&gt;"
    let u be htmlForSpot("u", rank)
    let v be htmlForSpot("v", symbol)
    variable grid set to ""
    each location in gridByRank[rank]
      if location is "royal" then
        set grid to grid + htmlForSpot(location, rank)
      else
        set grid to grid + htmlForSpot(location, symbol)
      end if
    end each
    set html to html + "{u}{v}{grid}"
  end if
  return html + "&lt;/div&gt;"
end function

function htmlForSpot(id as String, content as String) returns String
  return "&lt;div class='{id}'&gt;{content}&lt;/div&gt;"
end function

function strategyA(p as Player, dealerFaceUp as Card) returns Action
  return Action.stand
end function

function strategyB(p as Player, dealerFaceUp as Card) returns Action
  return Action.draw
end function

class Game
  constructor(dealerStartPoints as Int)
    set property.dealer to new Dealer(dealerStartPoints)
  end constructor

  property dealer as Dealer

  property players as List&lt;of Player&gt;

  property message as String

  procedure newRound()
    call property.dealer.newHand()
    each player in property.players
      call player.newHand()
    end each
  end procedure

  procedure updatePoints()
    each player in property.players
      let playerOutcome be determinePlayerOutcome(property.dealer, player)
      call global.updatePoints(property.dealer, player, playerOutcome)
    end each
  end procedure

  procedure addPlayer(player as Player)
    call property.players.append(player)
  end procedure

  procedure setMessage(message as String)
    set property.message to message
  end procedure

end class

class Card
  property suit as Suit

  property rank as String

  property faceDown as Boolean

  procedure turnFaceUp()
    set property.faceDown to false
  end procedure

  procedure turnFaceDown()
    set property.faceDown to true
  end procedure

end class

abstract class Player
  property name as String

  property points as Int

  property cards as List&lt;of Card&gt;

  property handTotal as Int

  property softAce as Boolean

  property status as Status

  property hasTurn as Boolean

  procedure startTurn()
    if property.status is Status.active then
      set property.hasTurn to true
    end if
  end procedure

  procedure evaluateStatus(newCard as Card)
    if (cardCount() is 2) and (property.handTotal is 21) then
      set property.status to Status.blackjack
    else if (property.handTotal &gt; 21) and (property.softAce) then
      set property.handTotal to property.handTotal - 10
      set property.softAce to false
    else if property.handTotal &gt; 21 then
      set property.status to Status.bust
    else if property.handTotal is 21 then
      set property.status to Status.standing
    end if
    if property.status isnt Status.active then
      set property.hasTurn to false
    end if
  end procedure

  procedure stand()
    set property.status to Status.standing
    set property.hasTurn to false
  end procedure

  procedure draw()
    let newCard be dealCard(random())
    call property.cards.append(newCard)
    if newCard.rank is "A" then
      call addAce()
    else
      set property.handTotal to property.handTotal + rankValue[newCard.rank]
    end if
    call evaluateStatus(newCard)
  end procedure

  procedure addAce()
    if property.softAce then
      set property.handTotal to property.handTotal + 1
    else
      set property.handTotal to property.handTotal + 11
      set property.softAce to true
    end if
  end procedure

  function cardCount() returns Int
    return property.cards.length()
  end function

  procedure changePointsBy(amount as Int)
    set property.points to property.points + amount
  end procedure

  abstract procedure newHand()

  procedure newHandHelper()
    set property.cards to empty List&lt;of Card&gt;
    set property.handTotal to 0
    set property.status to Status.active
    call draw()
    call draw()
  end procedure

  abstract function getMessage() returns String

  function messageHelper() returns String
    variable msg set to "- hand total: {property.handTotal}"
    let status be property.status
    if property.hasTurn then
      set msg to msg + " - PLAYING"
    else if status is Status.standing then
      set msg to msg + " - STANDING"
    else if status is Status.blackjack then
      set msg to msg + " - BLACKJACK"
    else if status is Status.bust then
      set msg to msg + " - BUST"
    end if
    return msg
  end function

  abstract procedure nextAction(dealerFaceCard as Card)

end class

class Dealer inherits Player
  constructor(startingPoints as Int)
    set property.name to "Dealer"
    set property.points to startingPoints
  end constructor

  property faceCard as Card

  procedure play()
    call startTurn()
    let hiddenCard be property.cards[1]
    call hiddenCard.turnFaceUp()
  end procedure

  procedure newHand()
    call newHandHelper()
    set property.faceCard to property.cards[0]
    let hiddenCard be property.cards[1]
    call hiddenCard.turnFaceDown()
  end procedure

  procedure nextAction(faceCard as Card)
    if property.handTotal &lt; 17 then
      call draw()
    else
      call stand()
    end if
  end procedure

  function getMessage() returns String
    return messageHelper()
  end function

end class

class HumanPlayer inherits Player
  constructor(name as String, startingPoints as Int)
    set property.name to name
    set property.points to startingPoints
  end constructor

  procedure newHand()
    call newHandHelper()
  end procedure

  procedure nextAction(dealerFaceCard as Card)
    variable key set to ""
    call clearKeyBuffer()
    while key is ""
      set key to waitForKey().lowerCase()
      if key is "d" then
        call draw()
      else if key is "s" then
        call stand()
      else
        set key to ""
      end if
    end while
  end procedure

  function getMessage() returns String
    variable msg set to messageHelper()
    if property.hasTurn then
      set msg to msg + " - press 'd' to draw, 's' to stand"
    end if
    return msg
  end function

end class

class AutomatedPlayer inherits Player
  constructor(name as String, decisionFunc as Func&lt;of Player, Card =&gt; Action&gt;, startingPoints as Int)
    set property.name to name
    set property.decisionFunc to ref decisionFunc
    set property.points to startingPoints
  end constructor

  property decisionFunc as Func&lt;of Player, Card =&gt; Action&gt;

  procedure newHand()
    call newHandHelper()
  end procedure

  procedure nextAction(dealerFaceCard as Card)
    let act be decisionFunc(this, dealerFaceCard)
    if act is Action.draw then
      call draw()
    else
      call stand()
    end if
  end procedure

  function getMessage() returns String
    return messageHelper()
  end function

end class

enum Action stand, draw

enum Outcome undecided, lose, draw, win, winDouble

enum Status active, standing, blackjack, bust

enum Suit clubs, diamonds, hearts, spades

constant rankValue set to {"2":2, "3":3, "4":4, "5":5, "6":6, "7":7, "8":8, "9":9, "10":10, "J":10, "Q":10, "K":10, "A":11}

constant symbols set to {Suit.clubs:"&amp;clubs;", Suit.diamonds:"&amp;diams;", Suit.hearts:"&amp;hearts;", Suit.spades:"&amp;spades;"}

constant colours set to {Suit.clubs:"black", Suit.diamonds:"red", Suit.hearts:"red", Suit.spades:"black"}

constant gridByRank set to {"A":{"royal"}, "2":{"b", "c"}, "3":{"a", "b", "c"}, "4":{"d", "e", "f", "g"}, "5":{"a", "d", "e", "f", "g"}, "6":{"d", "e", "f", "g", "h", "i"}, "7":{"d", "e", "f", "g", "h", "i", "l"}, "8":{"d", "e", "f", "g", "h", "i", "l", "m"}, "9":{"a", "d", "e", "f", "g", "n", "o", "p", "r"}, "10":{"d", "e", "f", "g", "n", "o", "p", "r", "s", "t"}, "J":{"d", "g", "royal"}, "Q":{"d", "g", "royal"}, "K":{"d", "g", "royal"}}

constant styleSheet set to ':root {\n    background-color: darkgreen;\n    padding-left: 5px;\n}\n\n.game .message, .player .details {\n    color: white;\n    font-family: Arial, Helvetica, sans-serif;\n}\n\n.hand {\n        margin-top: 5px;\n        height: 150px;\n        padding-bottom: 10px;\n    }\n    \n.card {\n    position: relative;\n    float: left;\n    background-color: white;\n    width: 95px;\n    height:140px;\n    margin-right:10px;\n    padding: 5px;\n    border-radius: 5px;\n    font-family: Helvetica, sans-serif; \n}\n.royal,.a,.b,.c,.d,.e,.f,.g,.h,.i,.j,.k,.l,.m,.n,.o,.p,.q,.r,.s,.t,.u,.v,.w,.x,.y,.z {position: absolute; text-align:center;}\n\n/* Standard spots */ \n    .a,.b,.c,.d,.e,.f,.g,.h,.i,.l,.m,.n,.o,.p,.r,.s,.t  {font-size:  30px;}\n\n    /* columns */\n    .d,.n,.h,.p,.f {left: 18px }\n    .a,.b,.c,.l,.m,.s,.t {left: 43px;}\n    .e,.o,.i,.r,.g {left: 68px}\n\n    /* rows */\n    .d,.b,.e {top: 0px}\n    .s {top: 20px;}\n    .l {top: 28px;}\n    .n,.o {top: 37px;}\n    .h,.a,.i {top: 57px}\n    .p,.r {top: 75px;}\n    .m {top: 86px;}\n    .t {top: 93px;}\n    .f,.c,.g {top: 114px;}\n\n/* royals */\n    .royal {\n        position: absolute;\n        z-index: 1;\n        width: 95px;\n        height: 140px;\n        line-height: 140px;\n        font-size: 100px;\n    }\n\n/* corner summary */\n    .u {font-size: 15px; width: 15px; text-align: center; left: 0px; top: 2px;}\n    .v {font-size: 20px; width: 15px; text-align: center; left: 0px; top: 12px;}\n\n/* suit colors */\n    .red {color: red}\n    .black {color: black}\n\n/* back */\n    .card.reversed { background-color: rgba(0, 0, 255, 0.607);}'
</div> and then immediately <b>file &gt; auto save</b> the
      code to your local file storage, because you will be making changes and want to ensure that your changes are saved.
      Then immediately <b>run</b> the program and note the following:<p></p>
    <ul>
      <li>There are two players: the dealer and Player A (you). Both have 1000 points at the start.</li>
      <li>Both have been dealt two cards, but one of the dealer's cards is initially dealt face down - which is known as the 'hole card'.</li>
      <li>Most cards have a numeric value (e.g. '7'). The Royal cards - Jack, Queen, King - each have
        the value 10. An Ace is special: it can be valued at 11, or at 1. An Ace valued at 11 is known as a 'soft Ace'. 
        So if a player's first two cards are <i>both</i> Aces then the starting hand total is counted as 12 (rather than 22).</li>
      <li>If the first two cards of any player total 21 - which would require one Ace, and one Royal or 10 card -
         then you have 'BLACKJACK' - the best hand possible - and so no further action is needed from you.</li>
      <li>Otherwise, when it is your turn you have the option to 'draw' another card - by hitting the <b>d</b> key - or
        to 'stand' - by hitting the 's' key.</li>
      The aim is to  get the value of your hand as close to 21 as you can - but if drawing a card causes
        the total to go over 21, 
        If your  total would exceed 21 and you have a soft Ace (see above), the Ace is automatically de-valued to 1. 
        If your  total would exceed 21 and you <i>don't</i> have a soft Ace (see above), then you are 'BUST' and you have no further role - for this round.
      <li>
      </li><li>When your status changes either to STANDING or BUST then play passes to the the next player or -
        if you are the last or only player - to the dealer.</li><li>
      </li><li>The dealer now plays out their hand, turning the 'hole card' face up and then following fixed rules about when to stand and when to draw.</li>
      <li>When all players, including the dealer, have finished their turn, the outcome is determined, and 
        points are passed between each player and the dealer as follows:
        <ul>
          <li>If a player is BUST then 1 point is passed over to the dealer <i>even the dealer is also BUST</i>.</li>
          <li>else, if a player has BLACKJACK then they gain 2 points from the dealer <i>unless</i> the dealer
          also has BLACKJACK, in which case it is a draw and no points are exchanged.</li>
          <li>else, if the player is STANDING and the dealer has BLACKJACK the dealer gains one point from the player.</li>
          <li>else, if the player is STANDING and their hand total is greater than that of the dealer, the player gains one point from the dealer.</li>
          <li>else, if the player is STANDING and their hand total is the same as that of the dealer it is a dealer, then it is a draw and
            no points are exchanged.</li>
          <li>else - meaning that the player's total is less than that of the dealer, then the dealer gains one point from the player.</li>
        </ul> 
      </li>
      <li>The updated points for the dealer and players are now shown, and you are given the option to play another round.</li>
      <li>This program works with an 'infinite deck' of cards: the chance of withdrawing a specific card - such as the 7 of clubs -
        is the same every time you draw a card, which means that there is a small chance that the same card can appear on
        screen more than once. (The same is true in most casinos, since they use multiple packs of cards in one deck,
        and some even use machines that shuffle the whole deck continuously.)
      </li>
    </ul>
    <p>So, play exactly 10 rounds of the game - so that you will hopefully observe the principles listed above.</p>
    <p>How many rounds did you play, and how many points did you, and the dealer, end up with?</p>
<textarea class="question" id="question1-0" placeholder="input is required"></textarea>
    <p>Finish playing the game - either by pressing the <b>n</b> key at the end of a round, or by clicking the <b>stop</b>
      button at any point while the program is running.</p>
<h4>Notes</h4><textarea class="notes" id="notes1" placeholder="Student or teacher may optionally add notes here"></textarea><label class="done" for="done1">Step Completed</label><input class="step-complete" type="checkbox" id="done1"><span> Total hints used: <span class="hints-taken"></span>/<span class="hints-total"></span></span></div>

<div class="step" id="step2"><h4>Notes</h4><textarea class="notes" id="notes2" placeholder="Student or teacher may optionally add notes here"></textarea><label class="done" for="done2">Step Completed</label><input class="step-complete" type="checkbox" id="done2"><span> Total hints used: <span class="hints-taken"></span>/<span class="hints-total"></span></span></div>tep>
  <h1>Step STEPNUMBER: starting to explore the code</h1>

  <p>To explore the code that defined the Blackjack application, the 'code editor' (the left-hand pane of the Elan screen) needs to have the browser's focus,
    which is indicated by having a <i>white</i> background. If the code editor has a pale-tinted background, 
    it does not currently have the browser focus. Provided that the program is not currently running (if it is, see above) you
    can put the focus on the code editor either:
    <ul>
      <li>from the keyboard by pressing <b>Ctrl-e</b> (think: <b>e</b>for <b>e</b>itor).</li>
      <li>with the mouse, by clicking within the code (it's best to click on the space, rather than the text), <i>or</i></li>
    </ul></p>

  <p>The code is made up of 'instructions' each of which has an instruction number on the right hand side of the editor. The
    number does <i>not</i> form part of the code - it is just to make it easier to refer to a particular instruction in discussions.</p>

  <p>Scroll to the bottom of the code either by:
    <ul>
      <li>using keyboard shortcuts: <b>PgDn</b>,<b>PgUp</b>,<b>Home</b>, <b>End</b></li>
      <li>using the mouse scroll-wheel, or dragging the slider bar on the right of the code pane</li>
    </ul></p>

  <question>How many instructions are there in the Blackjack program</question>

   <p>Notice that every instruction begins with a 'keyword' shown in dark blue - such as <el-code><el-kw>main</el-kw></el-code>, <el-code><el-kw>procedure</el-kw></el-code>, <el-code><el-kw>variable</el-kw></el-code>, <el-code><el-kw>call</el-kw></el-code>.
  This <i>initial</i> keyword defines the instruction, so we talk about, for example, a 'call instruction'.</p>

   <question>Find and list here at least five more, different, <i>initial</i> keywords:</question>

   <p>If an instruction has a pale tinted background - it has the 'focus' within the editor. Where the focus is within the editor does not
    change how the program operates, but you need to put the focus on an instruction in order to make any change to it, and sometimes
    it is useful to move the focus just to help you read the code, or to draw attention to a particular instruction in discussions.
    You can move the focus between instructions either:</p>

    
  <div hidden="" id="hash"></div>
  <div hidden="" id="changes"></div>

    <script src="https://elan-language.github.io/LanguageAndIDE/diff.js"></script>
    <script src="https://elan-language.github.io/LanguageAndIDE/worksheet-model-scripts.js"></script>
  </body>
</html>