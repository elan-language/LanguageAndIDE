# cd4c537ef76ac2affe80874c97e92c5490765294e0e8a18c42f26753ee5ee117 Elan 2.0.0-alpha guest default_profile valid

main
  variable fruit set to example
  call print(binarySearch(fruit, "lime"))
  call print(binarySearch(fruit, "orange"))
end main

function binarySearch(li as ListImmutable<of String>, item as String) returns Boolean
  variable result set to false
  if li.length() > 0 then
    variable mid set to (divAsInt(li.length(), 2)).floor()
    variable value set to li[mid]
    if item.isSameValueAs(value) then
      set result to true
    elif item.isBefore(value) then
      set result to binarySearch(li.subList(0, mid), item)
    else
      set result to binarySearch(li.subList(mid + 1, li.length()), item)
    end if
  end if
  return result
end function

test test_binarySearch
  variable li1 set to {"lemon", "lime", "orange"}
  assert binarySearch(li1, "lemon") is true
  assert binarySearch(li1, "lime") is true
  assert binarySearch(li1, "orange") is true
  assert binarySearch(li1, "pear") is false
  variable li2 set to {"lemon", "orange"}
  assert binarySearch(li2, "lemon") is true
  assert binarySearch(li2, "orange") is true
  assert binarySearch(li2, "pear") is false
  variable li3 set to {"lemon"}
  assert binarySearch(li3, "lemon") is true
  assert binarySearch(li3, "lime") is false
  variable li4 set to new List<of String>()
  assert binarySearch(li4, "pear") is false
end test

constant example set to {"apple", "avocado", "banana", "blueberry", "cherry", "fig", "grape", "kiwi", "lemon", "lychee", "mango", "orange", "papaya", "peach", "pear", "pineapple", "plum", "raspberry", "strawberry", "watermelon"}
