# 9122bfc4ad77b8ecea4a12008d95d8ffcfa8b66dbd2b1f60a561f5c5ef748f32 Elan 2.0.0-alpha guest default_profile valid

main
  let li be [1, 2, 3, 4, 5]
  call print(li.map(lambda n as Int => n^3))
  let names be ["Tom", "Dick", "Harriet"]
  call print(names.map(lambda s as String => s.upperCase()))
  call print(names.map(lambda s as String => reverseString(s)))
  call print(reverseStringsInList(names))
end main

function reverseStringsInList(l as List<of String>) returns List<of String>
  variable lReversed set to new List<of String>()
  variable sl set to 0
  each s in l
    set lReversed to lReversed.withAppend(reverseString(s))
  end each
  return lReversed
end function

function reverseString(s as String) returns String
  variable stringReversed set to ""
  for i from 0 to s.length() - 1 step 1
    set stringReversed to s[i] + stringReversed
  end for
  return stringReversed
end function

test test_reverseString
  assert reverseString("abcdefg") is "gfedcba"
  assert reverseStringsInList(["ab", "cde"]) is ["ba", "edc"]
end test
